subprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(16)
        }
    }

    test {
        useJUnitPlatform()
    }

    def ENABLE_PREVIEW = "--enable-preview"
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add(ENABLE_PREVIEW)
        options.release.set(16)

        // fix java 16 compilation
        options.forkOptions.jvmArgs.addAll(['--add-opens', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED'])
    }

    tasks.withType(Test).configureEach {
        jvmArgs(ENABLE_PREVIEW)
    }

    tasks.withType(JavaExec).configureEach {
        jvmArgs(ENABLE_PREVIEW)
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.1")
    }
}
